#!/usr/bin/env zsh

# vim:filetype=zsh syntax=zsh tabstop=2 shiftwidth=2 softtabstop=2 expandtab autoindent fileencoding=utf-8

################################################################################
# This file is sourced only for login shells. It is used to define the bare
# minimum shell functions that are needed during bootstrap of a vanilla OS to
# bootstrap installation of all other config files.
#
# file location: ${HOME}/.shellrc
# load order: .zshenv, .zprofile [.shellrc], .zshrc [.zshrc.custom [.aliases [.aliases.custom]]], .zlogin
################################################################################

# execute 'FIRST_INSTALL=true zsh' to debug the load order of the custom zsh configuration files
test -n "${FIRST_INSTALL+1}" && echo "loading ${0}"

NC='\033[0m' # No Color
BLACK='\033[0;30m'
DARK_GRAY='\033[1;30m'
RED='\033[0;31m'
LIGHT_RED='\033[1;31m'
GREEN='\033[0;32m'
LIGHT_GREEN='\033[1;32m'
ORANGE='\033[0;33m'
YELLOW='\033[1;33m'
BLUE='\033[0;34m'
LIGHT_BLUE='\033[1;34m'
PURPLE='\033[0;35m'
LIGHT_PURPLE='\033[1;35m'
CYAN='\033[0;36m'
LIGHT_CYAN='\033[1;36m'
LIGHT_GRAY='\033[0;37m'
WHITE='\033[1;37m'

blue() {
  printf "${LIGHT_BLUE}${1}${NC}"
}

cyan() {
  printf "${LIGHT_CYAN}${1}${NC}"
}

green() {
  printf "${GREEN}${1}${NC}"
}

red() {
  printf "${RED}${1}${NC}"
}

yellow() {
  printf "${YELLOW}${1}${NC}"
}

warn() (
  echo "$(yellow "**WARN** ${1}")"
)

non_zero_string() {
  ! test -z "${1}"
}

is_file() {
  non_zero_string "${1}" && test -f "${1}"
}

is_executable() {
  is_file "${1}" && test -e "${1}"
}

is_directory() {
  non_zero_string "${1}" && test -d "${1}"
}

is_git_repo() {
  is_directory "${1}" && test -d "${1}/.git"
}

load_file_if_exists() {
  # shellcheck disable=SC2015
  # shellcheck disable=SC1090
  is_executable "${1}" && source "${1}" || true
}

delete_directory_if_exists() {
  is_directory "${1}" && echo "$(red "Deleting") '$(green "${1}")'" && sudo rm -rf "${1}"
}

command_exists() {
  type "${1}" &> /dev/null 2>&1
}

# Note: This function is kind of equivalent to 'omz reload', but that doesn't seem to work when installing on a vanilla OS
load_zsh_configs() {
  command_exists omz && omz reload && return

  file_list=('.zshenv' '.zprofile' '.zshrc' '.zlogin')
  for file in "${file_list[@]}"; do
    FIRST_INSTALL=true load_file_if_exists "${HOME}/${file}"
  done
}

is_macos() {
  [[ "${OSTYPE}" =~ 'darwin' ]]
}

is_linux() {
  [[ "${OSTYPE}" =~ 'Linux' ]]
}

is_windows() {
  [[ "${OSTYPE}" =~ 'MINGW' ]]
}

if is_macos; then
  # Uninstall and reinstall xcode (useful immediately after upgrade or if reinstalling the OS)
  reinstall_xcode() {
    # delete if already present
    delete_directory_if_exists "/Applications/Xcode.app"

    xcode-select --install
    sudo xcodebuild -license accept || true
  }

  # Uninstall and reinstall xcode command line tools (useful immediately after upgrade or if reinstalling the OS)
  reinstall_xcode_cmdline_tools() {
    # delete if already present
    delete_directory_if_exists "/Library/Developer/CommandLineTools"

    # install using the non-gui cmd-line alone
    touch /tmp/.com.apple.dt.CommandLineTools.installondemand.in-progress
    softwareupdate -ia
    rm /tmp/.com.apple.dt.CommandLineTools.installondemand.in-progress
  }
fi
